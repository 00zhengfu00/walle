apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

def getRepositoryUrl() {
    return isSnapshot() ? SNAPSHOT_REPOSITORY_URL:RELEASE_REPOSITORY_URL
}

def isSnapshot() {
    return version.endsWith("SNAPSHOT");
}

def hasAndroidPlugin() {
    return getPlugins().inject(false) { a, b->
            def classStr = b.getClass().name
            def isAndroid = ("com.android.build.gradle.LibraryPlugin" == classStr) || ("com.android.build.gradle.AppPlugin" == classStr)
            a || isAndroid
        }
}
task sourcesJar(type: Jar) {
    if (hasAndroidPlugin()) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    } else {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
}
artifacts {
    archives sourcesJar
}

def nexusUser = ""
def nexusPassword = ""
File file = project.rootProject.file('local.properties');
if (file.exists()) {
    Properties properties = new Properties()
    properties.load(file.newDataInputStream());
    nexusUser = properties.getProperty("nexus.user")
    nexusPassword = properties.getProperty("nexus.password")
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: repositoryUrl) {
            authentication(userName: nexusUser, password: nexusPassword)
        }
    }
}